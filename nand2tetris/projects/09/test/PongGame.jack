// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/11/Pong/PongGame.jack

/**
 * Represents a Pong game.
 */
class PongGame {
	
	static int number;		  // the number of blocks left
    static PongGame instance; // the singelton, a Pong game instance     
    field Bat bat;            // the bat
    field Ball ball;          // the ball
    field int wall;           // the current wall that the ball is bouncing off of.
    field boolean exit;       // true when the game is over
    field int score;          // the current score.
    field int lastWall;       // the last wall that the ball bounced off of.
	field boolean win;
	field boolean start;
	field Array blockArr;

    // The current width of the bat
    field int batWidth;

    /** Constructs a new Pong game. */
    constructor PongGame new() {
		var int x,y,z;
		let x=0;
		let y=0;
		let z=0;
	    do Screen.clearScreen();
		let number =0;
        let batWidth = 50;  // initial bat size
        let bat = Bat.new(230, 229, batWidth, 7);
        let ball = Ball.new(253, 222, 0, 511, 0, 229);
        do ball.setDestination(400,0);
        do Screen.drawRectangle(0, 238, 511, 240);
		let blockArr=Array.new(32);
		while(number<24){
			let blockArr[number]=Block.new(x,y);
			let number=number+1;
			let z=z+1;
			let x=x+64;
			if(x>511){
				let x=0;
			}
			if(z>7){
				let z=0;
				let y=y+20;
			}
		}
	    do Output.moveCursor(22,0);
	    do Output.printString("Score: 0");
	
	    let exit = false;
	    let score = 0;
	    let wall = 0;
	    let lastWall = 0;
		let win=false;
		let start=false;
		
        return this;
    }

    /** Deallocates the object's memory. */
    method void dispose() {
        do bat.dispose();
	    do ball.dispose();
        do Memory.deAlloc(this);
        return;
    }

    /** Creates an instance of Pong game, and stores it. */
    function void newInstance() {
        let instance = PongGame.new();
        return;
    }
    
    /** Returns the single instance of this Pong game. */
    function PongGame getInstance() {
        return instance;
    }

    /** Starts the game, and andles inputs from the user that control
     *  the bat's movement direction. */
    method void run() {
        var char key; 
		while((~start)){
			do Output.moveCursor(10,20);
	        do Output.printString("Press space to start");
			let key = Keyboard.keyPressed();
			let start=(key=32);
			do Sys.wait(50);
		}
		if(start){
			do Screen.setColor(false);
			do Screen.drawRectangle(100,100,400,150);
		}
        while ((~exit)&(~win)) {
            // waits for a key to be pressed.
            while ((key = 0) & (~exit)&(~win)) {
                let key = Keyboard.keyPressed();
                do bat.move();
                do moveBall();
                do Sys.wait(50);
            }

            if (key = 130) { do bat.setDirection(1); }
	        else {
	            if (key = 132) { do bat.setDirection(2); }
		        else {
                    if (key = 140) { let exit = true; }
		        }
            }

            // Waits for the key to be released.
            while ((~(key = 0)) & (~exit)&(~win)) {
                let key = Keyboard.keyPressed();
                do bat.move();
                do moveBall();
                do Sys.wait(50);
            }
        }

	    if (exit) {
            do Output.moveCursor(10,27);
	        do Output.printString("Game Over");
	    }
		if (win) {
            do Output.moveCursor(10,27);
	        do Output.printString("You won!");
	    }   
        return;
    }

    /**
     * Handles ball movement, including bouncing.
     * If the ball bounces off a wall, finds its new direction.
     * If the ball bounces off the bat, increases the score by one
     * and shrinks the bat's size, to make the game more challenging. 
     */
    method void moveBall() {
        var int bouncingDirection, batLeft, batRight, ballLeft, ballRight,ballBottom,ballTop;
		var int i,j,k;
		var Block block;
		var boolean check;
		
        let ballRight = ball.getRight();
		let ballTop=ball.getTop();
		let i=ballRight/64;
		let j=ballTop/20;
		let k=8*j+i;
		if(k<24){
		let block=blockArr[k];
		let check=block.isExist();
		}
		else{
		let block=blockArr[0];
		let check=false;
		}
        let wall = ball.move(block);
        if ((wall > 0) & (~(wall = lastWall))&(~win)) {
            let lastWall = wall;
            let bouncingDirection = 0;
            let batLeft = bat.getLeft();
            let batRight = bat.getRight();
			let ballLeft = ball.getLeft();
			let ballRight = ball.getRight();
			let ballBottom=ball.getBottom();
  
            if ((wall = 4)&(ballBottom>227)) {
                let exit = (batLeft > ballRight) | (batRight < ballLeft);
                if (~exit) {
                    if (ballRight < (batLeft + 10)) { let bouncingDirection = -1; }
                    else {
                        if (ballLeft > (batRight - 10)) { let bouncingDirection = 1; }
                    }

                    let batWidth = batWidth - 2;
                    do bat.setWidth(batWidth);      
                    let score = score + 1;
                    do Output.moveCursor(22,7);
                    do Output.printInt(score);
                }
            }
            do ball.bounce(bouncingDirection,block);
			if(check){
				let number=number-1;
			}
			if (number=0){
				let win=true;
			}
			do Output.moveCursor(10,27);
	        do Output.printInt(number);
        }
        return;
    }
	
}